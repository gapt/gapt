reference defs.hlk;

define proof \varphi_{C-Union}
  with meta term x, y of type myset;

  proves
    C(x), C(y) :- C( x \cup y );

  with undef C
    O(compN(x)), O(compN(y)) :- O( compN(x \cup y) );
  with paramod
    left by proof DeMorgan_1( x, y )
    right by proof \varphi_{O-Union}( compN(x), compN(y) );
;

define proof \varphi_{O-Union}
  with meta term x, y of type myset;
  proves
    O(x), O(y) :- O( x \cap y );

  with undef O
    :- all m ( ( m \in x \cap y ) impl
            ( ex t ( \nu( m, t + 1 ) \subseteq x \cap y ) ) );
  with all right
    :- ( k_0 \in x \cap y ) impl
       ( ex t ( \nu( k_0, t + 1 ) \subseteq x \cap y ) );
  with impl right
    k_0 \in x \cap y :- ex t ( \nu( k_0, t + 1 ) \subseteq x \cap y );
  with undef O
    all m ( ( m \in x ) impl ( ex t ( \nu( m, t + 1 ) \subseteq x ) ) ) :- ;
  with all left
    k_0 \in x impl ex l ( \nu( k_0, l + 1 ) \subseteq x ) :- ;
  with impl left
    ex t ( \nu( k_0, t + 1 ) \subseteq x ) :-
    left by proof \varphi_{O-Union}-a( x, y );
  with ex left
    \nu( k_0, t_0 + 1 ) \subseteq x :- ;
  with undef O
    all m ( ( m \in y ) impl ( ex t ( \nu( m, t + 1 ) \subseteq y ) ) ) :- ;
  with all left
    k_0 \in y impl ex t ( \nu( k_0, t + 1 ) \subseteq y ) :- ;
  with impl left
    ex t ( \nu( k_0, t + 1 ) \subseteq y ) :-
    left by proof \varphi_{O-Union}-b( x, y );
  with ex left
    \nu( k_0, t_1 + 1 ) \subseteq y :- ;
  with ex right
    :- \nu( k_0, ((t_0 + 1) * t_1 + t_0) + 1 ) \subseteq x \cap y;
  with paramod by ((t_0 + 1) * t_1 + t_0) + 1 = (t_0 + 1) * (t_1 + 1)
    right :- \nu( k_0, (t_0 + 1) * (t_1 + 1) ) \subseteq x \cap y;
  with undef \subseteq
    :- all n ( n \in \nu( k_0, (t_0 + 1) * (t_1 + 1) ) impl n \in x \cap y );
  with all right
    :- m_0 \in \nu( k_0, (t_0 + 1) * (t_1 + 1) ) impl m_0 \in x \cap y;
  with impl right
    m_0 \in \nu( k_0, (t_0 + 1) * (t_1 + 1) ) :- m_0 \in x \cap y;
  with undef \cap
    :- m_0 \in x and m_0 \in y;
  with and right
    left by proof \psi'_1( x )
    right by proof \psi'_2( y );
;

define proof \varphi_{O-Union}-a
  with meta term x, y of type myset;
  proves
    k_0 \in x \cap y :- k_0 \in x;

  with undef \cap
    k_0 \in x and k_0 \in y :- ;
  with and left
    k_0 \in x :- ;
;

define proof \varphi_{O-Union}-b
  with meta term x, y of type myset;

  proves
    k_0 \in x \cap y :- k_0 \in y;

  with undef \cap
    k_0 \in x and k_0 \in y :- ;
  with and left
    k_0 \in y :- ;
;

define proof \psi'_1
  with meta term x of type myset;

  proves
    m_0 \in \nu( k_0, (t_0 + 1) * (t_1 + 1) ),
    \nu( k_0, t_0 + 1 ) \subseteq x
    :- m_0 \in x;

  with undef \subseteq
    all n ( (n \in \nu( k_0, t_0 + 1 ) ) impl (n \in x) ) :- ;
  with all left
    m_0 \in \nu( k_0, t_0 + 1 ) impl m_0 \in x :- ;
  with impl left
    :- m_0 \in \nu( k_0, t_0 + 1 )
    right auto propositional m_0 \in x :- m_0 \in x;
  with undef \nu
    ex r ( m_0 = k_0 + r * ( (t_0 + 1) * (t_1 + 1) ) ) :-
    ex r ( m_0 = k_0 + r * (t_0 + 1) );
  with ex left
    m_0 = k_0 + r_0 * ( (t_0 + 1) * (t_1 + 1) ) :- ;
  with ex right
    :- m_0 = k_0 + (r_0 * (t_1 + 1)) * (t_0 + 1);
  explicit axiom
    m_0 = k_0 + r_0 * ( (t_0 + 1) * (t_1 + 1) )
    :- m_0 = k_0 + (r_0 * (t_1 + 1)) * (t_0 + 1);
;

define proof \psi'_2
  with meta term y of type myset;

  proves
    m_0 \in \nu( k_0, (t_0 + 1) * (t_1 + 1) ),
    \nu( k_0, t_1 + 1 ) \subseteq y
    :- m_0 \in y;

  with undef \subseteq
    all n ( n \in \nu( k_0, t_1 + 1 ) impl n \in y ) :- ;
  with all left
    m_0 \in \nu( k_0, t_1 + 1 ) impl m_0 \in y :- ;
  with impl left
    :- m_0 \in \nu( k_0, t_1 + 1 )
    right auto propositional m_0 \in y :- m_0 \in y;
  with undef \nu
    ex r ( m_0 = k_0 + r * ( (t_0 + 1) * (t_1 + 1) ) ) :-
    ex r ( m_0 = k_0 + r * (t_1 + 1) );
  with ex left
    m_0 = k_0 + r_0 * ( (t_0 + 1) * (t_1 + 1) ) :- ;
  with ex right
    :- m_0 = k_0 + (r_0 * (t_0 + 1)) * (t_1 + 1);
  explicit axiom
    m_0 = k_0 + r_0 * ( (t_0 + 1) * (t_1 + 1) )
    :- m_0 = k_0 + (r_0 * (t_0 + 1)) * (t_1 + 1);
;

define proof DeMorgan_1
  with meta term x, y of type myset;
  proves
    :- compN(x \cup y) = compN(x) \cap compN(y);

  with undef equality of type myset, myset
    :- all n ( ( n \in compN(x \cup y) impl n \in compN(x) \cap compN(y) ) and
               ( n \in compN(x) \cap compN(y) impl n \in compN(x \cup y) ) );
  with all right
    :- ( n_0 \in compN(x \cup y) impl n_0 \in compN(x) \cap compN(y) ) and
       ( n_0 \in compN(x) \cap compN(y) impl n_0 \in compN(x \cup y) );
  with and right
    left by proof DeMorgan_{1b}( x, y )
    right by proof DeMorgan_{1a}( x, y );
;

define proof DeMorgan_{1a}
  with meta term x, y of type myset;
  proves
    :- n_0 \in compN(x) \cap compN(y) impl n_0 \in compN(x \cup y);

  with impl right
    n_0 \in compN(x) \cap compN(y) :- n_0 \in compN(x \cup y);
   with undef \cap
     n_0 \in compN(x) and n_0 \in compN(y) :- ;
  with undef compN
    :- not n_0 \in x \cup y;
  with auto propositional
    n_0 \in x \cup y, n_0 \in compN(x), n_0 \in compN(y) :- ;
  with undef \cup
    n_0 \in x or n_0 \in y :- ;
  with undef compN
    not n_0 \in x, not n_0 \in y :- ;
  continued auto propositional n_0 \in x or n_0 \in y, not n_0 \in x, not n_0 \in y :-;
;

define proof DeMorgan_{1b}
  with meta term x, y of type myset;
  proves
    :- n_0 \in compN(x \cup y) impl n_0 \in compN(x) \cap compN(y);

  with impl right
    n_0 \in compN(x \cup y) :- n_0 \in compN(x) \cap compN(y);
  with undef compN
    not n_0 \in x \cup y :- ;
  with not left
    :- n_0 \in x \cup y;
  with undef \cup
    :- n_0 \in x or n_0 \in y;
  with or right
    :- n_0 \in x, n_0 \in y;
  with undef \cap
    :- n_0 \in compN(x) and n_0 \in compN(y);
  with and right
    :- n_0 \in compN(x)
    right by proof DeMorgan_{1c}( x, y );
  with undef compN
    :- not n_0 \in x;
  continued auto propositional :- not n_0 \in x, n_0 \in x;
;

define proof DeMorgan_{1c}
  with meta term x, y of type myset;

  proves
    :- n_0 \in y, n_0 \in compN(y);
  with undef compN
    :- not n_0 \in y;
  continued auto propositional :- n_0 \in y, not n_0 \in y;
;
